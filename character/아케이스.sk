on script load:
	set {아케이스::체력} to 1000
	set {아케이스::이동속도} to 1.0
	set {아케이스::히트박스} to 1.0

	set {아케이스::평::피해량} to 75
	set {아케이스::평::피해감소거리} to 25

	set {아케이스::P1::달리기속도} to 30

	set {아케이스::2::쿨타임} to 7
	set {아케이스::2::직격피해량} to 50
	set {아케이스::2::폭발피해량} to 100
	set {아케이스::2::범위} to 3.5

	set {아케이스::3::쿨타임} to 12
	set {아케이스::3::회복량} to 350
	set {아케이스::3::지속시간} to 2.5

	set {아케이스::4::충전량} to 7000
	set {아케이스::4::지속시간} to 12

	set {아케이스::점수::미사일} to 3
	set {아케이스::점수::회복} to 8
	set {아케이스::점수::궁극기 보너스} to 40

	set {아케이스::패시브::이름1} to "강화된 신체"
	set {아케이스::패시브::1} to "||&f달리기의 &e⬆&b➠ 속도&f가 빨라집니다.||||&e⬆&b➠ &f30％"
	set {아케이스::액티브::이름2} to "D.I.A. 코어 미사일"
	set {아케이스::액티브::2} to "||&f소형 미사일을 3회 연속으로 발사하여 &c⚔ 광역 피해||&f를 입힙니다.||||&c⚔ &f직격 50 + 폭발 100||&c✸ &f3.5m||&f⟳ &f7초||||&7&l[2] [좌클릭] &f사용"
	set {아케이스::액티브::이름3} to "생체 회복막"
	set {아케이스::액티브::3} to "||&6⌛ 지속시간&f동안 동안 회복막을 활성화하여 &a✚ 회복&f합니다.||||&6⌛ &f2.5초||&a✚ &f350||&f⟳ &f12초||||&7&l[3] &f사용"
	set {아케이스::액티브::이름4} to "인피니버스터"
	set {아케이스::액티브::4} to "||&6⌛ 지속시간&f동안 기본 무기에 장탄수 무한, 탄퍼짐 제거, 거리별||&f피해 감소 제거 효과가 적용됩니다.||||&6⌛ &f12초||&f⚡ &f7000||||&7&l[4] &f사용"
function 아케이스_평_반동(player: player):
	set {_hrecoil} to random number between -0.15 to 0.2
	set {_vrecoil} to -0.7
	loop 2 times:
		if loop-number = 1:
			if getCooldown("%{_player}'s id%-gunshot") = 0 ticks:
				set {_hrecoil} to {_hrecoil}*2
				set {_vrecoil} to {_vrecoil}*2
			set {_hrecoil} to {_hrecoil}*2
			set {_vrecoil} to {_vrecoil}*2
		if loop-number = 2:
			set {_hrecoil} to {_hrecoil}/2
			set {_vrecoil} to {_vrecoil}/2
		if getCooldown("%{_player}'s id%-gunspread") <= 0.6 seconds:
			changeSight({_player}, {_hrecoil}/3, {_vrecoil}/3)
		else:
			changeSight({_player}, ({_hrecoil}+0.1)/3, {_vrecoil}/3)
		wait 1 tick
function 아케이스_평_효과음(player: player):
	if isUsingSkill({_player}, 3) is true:
		playSound("new.block.beacon.deactivate", {_player}, 4, 2)
		playSound("random.energy", {_player}, 4, 1.6)
		playSound("random.gun_reverb", {_player}, 5, 1.2)
		wait 8 tick
		playSound("entity.magmacube.jump", {_player}, 0.8, random number between 0.975 to 1.025)
	else:
		playSound("random.gun2.scarlight_1", {_player}, 3, 1)
		playSound("random.gun_reverb", {_player}, 5, 1.2)
		wait 8 tick
		playSound("entity.magmacube.jump", {_player}, 0.8, random number between 0.975 to 1.025)
function 아케이스_평(player: player):
	setItemCooldown({_player}, 2 ticks)
	아케이스_평_효과음({_player})
	set {_ploc} to location of {_player}'s head
	set {_loc} to location of {_ploc}
	if isUsingSkill({_player}, 3) is true:
		set {_dir} to getLineOfSight({_player})
	else:
		아케이스_평_반동({_player})
		set {_spread} to gunSpread({_player}, 5, 2.5, 3 ticks, 6 ticks)
		set {_dir} to vSpread(getLineOfSight({_player}), {_spread})

	while distance between {_ploc} and {_loc} < {Skill::MAX_RANGE}:
		add 1 to {_i}
		if isNonSolid({_loc}~{_dir}) is false:
			set {__loc} to location of {_loc}
			set {_hitloc} to {_loc}~{_dir}
			loop 4 times:
				if isNonSolid({__loc}~vMul({_dir}, 0.2)) is true:
					set {__loc} to {__loc}~vMul({_dir}, 0.2)
			bulletHit({__loc}, block at {_hitloc})
			exit loop

		if distance between {_ploc} and {_loc} > 0.5:
			set {_target} to nearEnemy({_player}, {_loc}, {Skill::HITS_HITBOX})
			if {_target} is set:
				set {__loc} to location of {_loc}
				loop 10 times:
					set {__loc} to {__loc}~vMul({_dir}, 0.75)
					if isHeadshot({__loc}, {_target}) is true:
						set {_headshot} to true
				exit loop

		set {_loc} to {_loc}~{_dir}
		if mod({_i}, 7) = 0:
			if isUsingSkill({_player}, 3) is true:
				particleRGB(location 0.2 right location 0.2 down {_loc}, 0, 0, 0, 0, 255, 255, 1)
			else:
				particle("crit", location 0.2 right location 0.2 down {_loc}, 0, 0, 0, 0, 1)

	if {_target} is set:
		set {_dmg} to getDistantDamage(skill({_player}, "평::피해량"), skill({_player}, "평::피해감소거리"), distance between {_player}'s head and {_target}'s head)
		if isUsingSkill({_player}, 3) is true:
			set {_dmg} to skill({_player}, "평::피해량")
			set {_type} to "normal-4"
		else:
			set {_type} to "normal-평"
		damage({_player}, {_target}, {_dmg}, {_loc}~{_dir}, "%{_type}%", "%{_headshot}%" parsed as boolean)
function 아케이스_2_발동(player: player):
	set {_ploc} to location of {_player}'s head
	set {_loc} to location 0.2 left location 0.4 down {_ploc}
	set {_dir} to vSpread(getLineOfSight({_player}), 1.5)
	set {_speed} to 60

	while distance between {_ploc} and {_loc} < {Skill::MAX_RANGE}:
		if getChar({_player}) = "아케이스":
			add 1 to {_i}
			if isNonSolid({_loc}~{_dir}) is false:
				exit loop

			if distance between {_ploc} and {_loc} > 0.5:
				set {_target} to nearEnemy({_player}, {_loc}, {Skill::PROJ_HITBOX})
				if {_target} is set:
					exit loop

			set {_loc} to {_loc}~{_dir}
			if mod({_i}, 5) = 0:
				particleRGB({_loc}, 0, 0, 0, 32, 250, 225, 1)
				particle("critmagic", {_loc}, 0, 0, 0, 0, 1)
			if mod({_i}/4, {_speed}/20) = 0:
				wait 1 tick
		else:
			exit

	playSound("entity.firework.large_blast", {_loc}, 4, 0.8)
	playSound("entity.generic.explode", {_loc}, 4, 1.3)
	playSound("entity.generic.explode", {_loc}, 4, 1.6)
	playSound("random.gun_reverb2", {_loc}, 6, 0.9)
	particle("explosion", {_loc}, 0.4, 0.4, 0.4, 0.2, 40)
	particleRGB({_loc}, 1.7, 1.7, 1.7, 32, 250, 225, 180)

	if {_target} is set:
		damage({_player}, {_target}, skill({_player}, "2::직격피해량"), {_loc}~{_dir})
	if distance between location 1 up {_player} and {_loc} < skill({_player}, "2::범위"):
		if canPass({_loc}, location of {_player}'s head) is true:
			if canPassShield({_player}, {_loc}, location of {_player}'s head) is true:
				damage({_player}, {_player}, skill({_player}, "2::폭발피해량"), {_player}, "normal-2")
				knockback({_player}, getLine(location 0.5 down {_loc}, {_player}, 0.3))
	loop nearEnemys({_player}, {_loc}, skill({_player}, "2::범위")):
		if canPass({_loc}, location of loop-entity's head) is true:
			if canPassShield({_player}, {_loc}, location of loop-entity's head) is true:
				damage({_player}, loop-entity, skill({_player}, "2::폭발피해량"), loop-entity, "normal-2")
				knockback(loop-entity, getLine(location 0.5 down {_loc}, loop-entity, 0.3))
function 아케이스_2(player: player):
	setSkillDuration({_player}, 1, -1)
	setItemCooldown({_player}, 12 ticks, true)

	loop 3 times:
		if getChar({_player}) = "아케이스":
			아케이스_2_발동({_player})
			playSound("random.gun.grenade", {_player}, 3, 1.5)
			playSound("entity.shulker.shoot", {_player}, 3, 1.2)
			wait 6 tick
		if isStunned({_player}) is true:
			exit loop

	if getChar({_player}) = "아케이스":
		setSkillCooldown({_player}, 1, rounded skill({_player}, "2::쿨타임"))
function 아케이스_3(player: player):
	setSkillDuration({_player}, 2, -1)
	setCooldown("%{_player}'s id%-3-지속시간", parseTimespan(skill({_player}, "3::지속시간")*20))
	playSound("entity.experience_orb.pickup", {_player}, 1.5, 0.9)
	playSound("item.armor.equip_diamond", {_player}, 1.5, 1.4)
	playSound("item.armor.equip_diamond", {_player}, 1.5, 1.2)

	loop skill({_player}, "3::지속시간")*20 times:
		if getChar({_player}) = "아케이스":
			heal({_player}, {_player}, skill({_player}, "3::회복량")/(skill({_player}, "3::지속시간")*20), {아케이스::점수::회복}/skill({_player}, "3::지속시간")/20)
			set {_dir1} to vMul(vector from yaw loop-number*10 and pitch 0, 1)
			set {_dir2} to vMul(vector from yaw loop-number*10+120 and pitch 0, 1)
			set {_dir3} to vMul(vector from yaw loop-number*10+240 and pitch 0, 1)
			set {_loc} to location 1 up {_player}
			set {_loc1} to {_loc}~{_dir1}
			set {_loc2} to {_loc}~{_dir2}
			set {_loc3} to {_loc}~{_dir3}
			particleRGB({_loc1}, 0, 0.4, 0, 220, 255, 36, 3)
			particleRGB({_loc2}, 0, 0.4, 0, 190, 255, 36, 3)
			particleRGB({_loc3}, 0, 0.4, 0, 160, 255, 36, 3)
			wait 1 tick
		if isStunned({_player}) is true:
			setCooldown("%{_player}'s id%-3-지속시간", 0 ticks)
			exit loop

	if getChar({_player}) = "아케이스":
		setSkillCooldown({_player}, 2, rounded skill({_player}, "3::쿨타임"))
function 아케이스_4(player: player):
	setSkillDuration({_player}, 3, rounded skill({_player}, "4::지속시간"))
	setCooldown("%{_player}'s id%-4-지속시간", parseTimespan(skill({_player}, "4::지속시간")*20))
	useUlt({_player})
	set slot 4 of {_player} to gun "HLN-12_인피니버스터" with no nbt

	loop skill({_player}, "4::지속시간")*20+1 times:
		if getChar({_player}) = "아케이스":
			wait 1 tick

	if getChar({_player}) = "아케이스":
		set slot 4 of {_player} to getWeapon("아케이스")