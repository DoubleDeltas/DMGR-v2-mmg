on script load:
	set {퀘이커::체력} to 2500
	set {퀘이커::이동속도} to 0.85
	set {퀘이커::히트박스} to 1.7

	set {퀘이커::평::피해량} to 300
	set {퀘이커::평::공격속도} to 1.2
	set {퀘이커::평::사거리} to 3

	set {퀘이커::P1::감소량} to 40

	set {퀘이커::1::쿨타임} to 3
	set {퀘이커::1::내구도} to 5000
	set {퀘이커::1::내구도회복} to 12

	set {퀘이커::2::쿨타임} to 25
	set {퀘이커::2::피해량} to 200
	set {퀘이커::2::사거리} to 10
	set {퀘이커::2::기절시간} to 1.6

	set {퀘이커::3::쿨타임} to 10
	set {퀘이커::3::충돌피해량} to 200
	set {퀘이커::3::기절시간} to 0.6
	set {퀘이커::3::사거리} to 40

	set {퀘이커::4::충전량} to 7000
	set {퀘이커::4::피해량} to 250
	set {퀘이커::4::사거리} to 12
	set {퀘이커::4::기절시간} to 1
	set {퀘이커::4::속박시간} to 8

	set {퀘이커::점수::피해 막음} to 0.015
	set {퀘이커::점수::기절-2} to 15
	set {퀘이커::점수::처치 지원-2} to 25
	set {퀘이커::점수::돌풍 강타} to 8
	set {퀘이커::점수::기절-3} to 8
	set {퀘이커::점수::기절-4} to 20
	set {퀘이커::점수::처치 지원-4} to 50

	set {퀘이커::점수::불굴의 방패 파괴} to 20

	set {퀘이커::패시브::이름1} to "불굴"
	set {퀘이커::패시브::1} to "||&f받는 &e⬇&5＠ 군중 제어 시간&f이 감소됩니다.||||&e⬇&5＠ &f40％"
	set {퀘이커::액티브::이름1} to "불굴의 방패"
	set {퀘이커::액티브::1} to "||&7방패&f를 펼쳐 전방의 공격을 방어합니다.||||&f⟳ &f파괴 시 3초||||&7방패||&a❤ &f5000||||&7&l[1] [우클릭] &f사용 / 해제"
	set {퀘이커::액티브::이름2} to "충격파 일격"
	set {퀘이커::액티브::2} to "||&f바닥을 내려찍어 충격파를 일으켜 &c⚔ 광역 피해&f를 입히고||&5＠ 기절&f시킵니다.||||&c⚔ &f200||&c⬌ &f10m||&5＠ &f1.6초||&f⟳ &f25초||||&7&l[2] &f사용"
	set {퀘이커::액티브::이름3} to "돌풍 강타"
	set {퀘이커::액티브::3} to "||&f적을 가둬 끌고가는 검기를 날립니다.||&f적이 벽에 충돌하면 &c⚔ 피해&f를 입고 &5＠ 기절&f합니다.||||&c⚔ &f200||&c⬌ &f40m||&5＠ &f0.6초||&f⟳ &f10초||||&7&l[3] &f사용"
	set {퀘이커::액티브::이름4} to "심판의 문지기"
	set {퀘이커::액티브::4} to "||&f방패를 강하게 찍어 충격파를 일으켜 &c⚔ 광역 피해&f를 입히고||&5＠ 기절&f시키고 &5✘ 속박&f시켜 &5➲ 밀쳐냅니다&f.||&f방패를 펼친 상태에서만 사용할 수 있습니다.||||&c⚔ &f250||&c⬌ &f12m||&5＠ &f1초||&5✘ &f8초||&f⚡ &f7000||||&7&l[4] &f사용"
function 퀘이커_평_반동(player: player):
	if getMetadata({_player}, "swing") is not set:
		wait 1 tick
		loop 2 times:
			changeSight({_player}, 0.9, 0.2)
			wait 1 tick
		loop 3 times:
			changeSight({_player}, 1.7, 0.4)
			wait 1 tick
		changeSight({_player}, -1, -0.8)
	else:
		wait 1 tick
		loop 2 times:
			changeSight({_player}, -0.9, 0.2)
			wait 1 tick
		loop 3 times:
			changeSight({_player}, -1.7, 0.4)
			wait 1 tick
		changeSight({_player}, 1, -0.8)
function 퀘이커_평_경직(player: player, target: entity, vel: vector):
	loop 3 times:
		knockback({_target}, vDiv({_vel}, 3), true)
		wait 1 tick
function 퀘이커_평(player: player):
	setItemCooldown({_player}, parseTimespan(skill({_player}, "평::공격속도")*20))
	setItemCooldown({_player}, parseTimespan(skill({_player}, "평::공격속도")*8), true)
	apply mining fatigue of tier 7 without particles to {_player} for parseTimespan(skill({_player}, "평::공격속도")*20) replacing existing effect
	if getMetadata({_player}, "swing") is not set:
		setMetadata({_player}, "swing", true)
		set {_swing} to true
	else:
		resetMetadata({_player}, "swing")
	playSound("random.gun2.shovel_leftclick", {_player}, 2, 0.6)
	wait 2 tick
	퀘이커_평_반동({_player})

	loop 8 times:
		if getChar({_player}) = "퀘이커":
			set {_ploc} to location of {_player}'s head
			set {_loc} to location of {_ploc}
			set {_axis} to getYawAxis({_loc})
			set {_dir} to vMul(getPitchAxis({_loc}), 0.3)
			if {_swing} is not set:
				rotate {_dir} around {_axis} by loop-number*20 degrees
			else:
				rotate {_dir} around {_axis} by 180-loop-number*20 degrees
			set {_i} to 0

			while distance between {_ploc} and {_loc} < skill({_player}, "평::사거리"):
				add 1 to {_i}
				if isNonSolid({_loc}~{_dir}) is false:
					set {__loc} to location of {_loc}
					set {_loc} to {_loc}~{_dir}
					loop 4 times:
						if isNonSolid({__loc}~vMul({_dir}, 0.2)) is true:
							set {__loc} to {__loc}~vMul({_dir}, 0.2)
					playSound("entity.player.attack.strong", {__loc}, 0.4, 0.8)
					meleeHit({__loc}, block at {_loc}, true)
					exit loop

				if distance between {_ploc} and {_loc} > 0.5:
					set {_target} to nearEnemy({_player}, {_loc}, {Skill::MELEE_HITBOX})
					if {_target} is not set:
						set {_target} to nearEnemy({_player}, location 0.5 down {_loc}, {Skill::MELEE_HITBOX})
					if {_target} is set:
						if canPassShield({_player}, location of {_player}'s head, location of {_target}'s head) is true:
							if getMetadata({_target}, "shield") is true:
								set {_tid} to getMetadata({_target}, "shieldNo")
							else:
								set {_tid} to {_target}'s id
							if {_targets::%{_tid}%} is not set:
								set {_targets::%{_tid}%} to {_target}
								damage({_player}, {_target}, skill({_player}, "평::피해량"), {_loc}~{_dir}, "normal-평")
								if {_swing} is not set:
									퀘이커_평_경직({_player}, {_target}, getLine({_loc}, location 0.6 right {_loc}, 0.6))
								else:
									퀘이커_평_경직({_player}, {_target}, getLine({_loc}, location -0.6 right {_loc}, 0.6))
								particle("crit", {_loc}~{_dir}, 0, 0, 0, 0.4, 20)
								playSound("entity.zombie.attack_iron_door", {_target}, 1, 0.8)
								playSound("entity.player.attack.strong", {_target}, 1, 0.7)
								playSound("entity.player.attack.crit", {_target}, 1, 0.8)
						delete {_target}

				set {_loc} to {_loc}~{_dir}
				if {_i} = 5:
					particleRGB(location 0.3 down {_loc}, 0.2, 0.2, 0.2, 200, 200, 200, 10)
				if {_i} = 7:
					particleRGB(location 0.3 down {_loc}, 0.2, 0.2, 0.2, 200, 200, 200, 10)
				if {_i} = 9:
					particle("crit", location 0.3 down {_loc}, 0.1, 0.1, 0.1, 0.1, 20)
				if {_i} = 1:
					if mod(loop-number, 2) = 0:
						playSound("random.swing", {_loc}~vMul({_dir}, 4), 2, 0)
						playSound("random.gun2.shovel_leftclick", {_loc}~vMul({_dir}, 4), 2, 0.6)
			if loop-number = 1:
				wait 2 tick
			if loop-number = 2 or 3 or 6 or 7:
				wait 1 tick
			if isStunned({_player}) is true:
				exit loop
function 퀘이커_1_발동(player: player, mob: entity):
	wait 1 tick
	while {_mob} is alive:
		if isUsingSkill({_player}, 0) is true:
			add 1 to {_i}
			if {_player}'s pitch > 30:
				set {_pitch} to 30
			else if {_player}'s pitch < -40:
				set {_pitch} to -40
			else:
				set {_pitch} to {_player}'s pitch
			set {_loc} to location 0.2 up {_player}'s location
			set pitch of {_loc} to 0
			(getMetadata({_player}, "1-개체1")).teleport(location 1.7 forwards location 1.6 left location -1+{_pitch}*0.023 down {_loc})
			(getMetadata({_player}, "1-개체2")).teleport(location 1.7 forwards location 0 left location -1+{_pitch}*0.023 down {_loc})
			(getMetadata({_player}, "1-개체3")).teleport(location 1.7 forwards location -1.6 left location -1+{_pitch}*0.023 down {_loc})
			set {_sloc} to location 1 down (getMetadata({_player}, "1-개체2")).getLocation()

			if mod({_i}, 4) = 0:
				delete {_sloc::*}
				add location 2.5 left location 0 up {_sloc} to {_sloc::*}
				add location -2.5 left location 0 up {_sloc} to {_sloc::*}
				add location -2.5 left location 2.7 up {_sloc} to {_sloc::*}
				add location 2.5 left location 2.7 up {_sloc} to {_sloc::*}
				add location 2.5 left location 0 up {_sloc} to {_sloc::*}
				loop path between {_sloc::*} with density 3:
					particle("crit", loop-location, 0, 0, 0, 0, 1)
			if parseNumber("%getMetadata({_player}, "1-내구도")%") <= skill({_player}, "1::내구도")/2:
				particleBlock(iron block, location random number between -2 to 2 left location 1.5 up {_sloc}, 0, 0.5, 0, 0.1, 1)
			if parseNumber("%getMetadata({_player}, "1-내구도")%") <= skill({_player}, "1::내구도")/4:
				particleBlock(iron block, location random number between -2 to 2 left location 1.5 up {_sloc}, 0, 0.5, 0, 0.1, 2)
			wait 1 tick
			if isStunned({_player}) is true:
				setSkillCooldown({_player}, 0, 0)
				exit loop
		else:
			exit loop

	if getChar({_player}) = "퀘이커":
		(getMetadata({_player}, "1-개체1")).remove()
		(getMetadata({_player}, "1-개체2")).remove()
		(getMetadata({_player}, "1-개체3")).remove()
		setCooldown("%{_player}'s id%-nomelee", 0 ticks)
		setCooldown("%{_player}'s id%-nojump", 0 ticks)
		setCooldown("%{_player}'s id%-nosprint", 0 ticks)
		playSound("block.piston.contract", {_player}, 1, 0)
function 퀘이커_1(player: player):
	if isUsingSkill({_player}, 0) is false:
		setSkillDuration({_player}, 0, -1)
		setItemCooldown({_player}, 8 ticks, true)
		setCooldown("%{_player}'s id%-nomelee", 1 days)
		setCooldown("%{_player}'s id%-nojump", 1 days)
		setCooldown("%{_player}'s id%-nosprint", 1 days)
		(getMetadata({_player}, "1-개체1")).remove()
		(getMetadata({_player}, "1-개체2")).remove()
		(getMetadata({_player}, "1-개체3")).remove()
		playSound("block.piston.extend", {_player}, 1, 0)
		playSound("entity.enderdragon.flap", {_player}, 1, 0.6)
		playSound("entity.enderdragon.flap", {_player}, 1, 0.6)
		set {_rno} to random integer between 0 to 1000

		loop 3 times:
			if loop-number = 1:
				spawn a iron golem at location 1.7 forwards location 1.6 left {_player}
			if loop-number = 2:
				spawn a iron golem at location 1.7 forwards location 0 left {_player}
			if loop-number = 3:
				spawn a iron golem at location 1.7 forwards location -1.6 left {_player}
			set {_mob} to spawned iron golem
			set name of spawned iron golem to "&b%{_player}%"
			apply invisibility of tier 1 without particles to {_mob} for 1 days
			if loop-number = 2:
				퀘이커_1_발동({_player}, {_mob})
			setTeam({_mob}, getTeam({_player}))
			setMetadata({_mob}, "owner", {_player})
			setMetadata({_mob}, "mobType", "불굴의방패")
			setMetadata({_mob}, "shield", true)
			setMetadata({_mob}, "shieldNo", {_rno})
			setMetadata({_player}, "1-개체%loop-number%", {_mob})
			entityTick({_mob})
			{_mob}.setSilent(true)
			{_mob}.setAI(false)
		# wait 1 tick
		# loop 3 times:
		# 	(getMetadata({_player}, "불굴의방패%loop-number%")).setAI(false)
	else:
		setSkillCooldown({_player}, 0, 0)
		setItemCooldown({_player}, 4 ticks, true)
		(getMetadata({_player}, "1-개체1")).remove()
		(getMetadata({_player}, "1-개체2")).remove()
		(getMetadata({_player}, "1-개체3")).remove()
function 퀘이커_2_발동(player: player):
	loop 7 times:
		set {_ploc} to location of {_player}
		set pitch of {_ploc} to 0
		set {_loc::%loop-number%} to location 1 forwards location 0.1 up {_ploc}
		set {_axis} to getYawAxis({_loc::%loop-number%})
		set {_dir::%loop-number%} to vMul(getPitchAxis({_loc::%loop-number%}), 0.5)
		rotate {_dir::%loop-number%} around {_axis} by 90+(9*(loop-number-4)) degrees
	particle("crit", location 0.1 up location 1 horizontally forwards {_player}, 0.2, 0, 0.2, 0.5, 100)

	loop 40 times:
		if getChar({_player}) = "퀘이커":

			loop 7 times:
				set {_loc} to {_loc::%loop-number-2%}
				set {_dir} to {_dir::%loop-number-2%}
				if isNonSolid({_loc}~{_dir}) is false:
					loop 3*5 times:
						if isNonSolid(location loop-number-3*0.2 up {_loc}~{_dir}) is true:
							set {_loc} to location loop-number-3*0.2 up {_loc}~{_dir}
							exit loop
				if isNonSolid(location 0.3 down {_loc}~{_dir}) is true:
					loop 3*5 times:
						if isNonSolid(location loop-number-3*0.2 down {_loc}~{_dir}) is false:
							set {_loc} to location loop-number-3*0.2 down location 0.2 up {_loc}~{_dir}
							exit loop
				if (check [isNonSolid({_loc}) is true] -> [isNonSolid(location 0.3 down {_loc}) is false]) is true:
					set {_loc::%loop-number-2%} to {_loc}
				else:
					delete {_loc::%loop-number-2%}
				if distance between {_ploc} and {_loc} >= skill({_player}, "2::사거리"):
					delete {_loc::%loop-number-2%}

				if {_loc::%loop-number-2%} is set:
					set {_target} to nearEnemy({_player}, location 0.3 up {_loc}, {Skill::MELEE_HITBOX})
					if {_target} is set:
						if getMetadata({_target}, "shield") is true:
							set {_tid} to getMetadata({_target}, "shieldNo")
						else:
							set {_tid} to {_target}'s id
						if {_targets::%{_tid}%} is not set:
							set {_targets::%{_tid}%} to {_target}
							if {_target} is a player:
								setCooldown("%{_player}'s id%-sup-%{_target}'s id%-normal-2", parseTimespan(skill({_player}, "2::기절시간")*20+20))
							stun({_player}, {_target}, skill({_player}, "2::기절시간"), {퀘이커::점수::기절-2})
							damage({_player}, {_target}, skill({_player}, "2::피해량"), {_target}, "normal-2")
							particle("crit", location (hitbox length of {_target}/2) up {_target}, 0, 0, 0, 0.4, 40)
							playSound("entity.player.attack.strong", {_target}, 1, 0.7)
							playSound("entity.player.attack.strong", {_target}, 1, 0.7)
						if getMetadata({_target}, "shield") is true:
							delete {_loc::%loop-number-2%}
							continue
					set {_loc::%loop-number-2%} to {_loc}~{_dir}
					particle("crit", {_loc::%loop-number-2%}, 0.3, 0, 0.3, 0.05, 5)
					if mod(loop-number-1, 3) = 0:
						meleeHit({_loc}, block at location 0.5 down {_loc::%loop-number-2%}, true, false)
					# particleBlock({_block}, {_loc::%loop-number-2%}, 0.3, 0, 0.3, 0.15, 3)
			if mod(loop-number, 2) = 0:
				wait 1 tick
function 퀘이커_2(player: player):
	setSkillDuration({_player}, 1, -1)
	setItemCooldown({_player}, 999 seconds, true)

	loop 10 times:
		if getChar({_player}) = "퀘이커":
			setCooldown("%{_player}'s id%-stop", 2 ticks)
			set {_axis} to getPitchAxis({_player})
			set {_ploc} to location of {_player}'s head
			set pitch of {_ploc} to 0
			set {_loc} to location of {_ploc}
			set {_dir} to vMul(getYawAxis({_loc}), -0.3)
			rotate {_dir} around {_axis} by -30+loop-number*17 degrees
			set {_i} to 0

			while distance between {_ploc} and {_loc} < skill({_player}, "평::사거리"):
				add 1 to {_i}
				if isNonSolid({_loc}~{_dir}) is false:
					exit loop

				set {_loc} to {_loc}~{_dir}
				if {_i} = 5:
					particleRGB(location 0.5 down {_loc}, 0.15, 0.15, 0.15, 200, 200, 200, 10)
				if {_i} = 7:
					particleRGB(location 0.5 down {_loc}, 0.15, 0.15, 0.15, 200, 200, 200, 10)
				if {_i} = 9:
					particle("crit", location 0.5 down {_loc}, 0.1, 0.1, 0.1, 0.1, 20)
				if {_i} = 1:
					playSound("random.swing", {_loc}~vMul({_dir}, 4), 2, 0)
					playSound("random.gun2.shovel_leftclick", {_loc}~vMul({_dir}, 4), 2, 0.6)

			if loop-number = 1 or 2:
				wait 2 tick
			else if loop-number = 3 or 4 or 5 or 6 or 7:
				wait 1 tick
			if isStunned({_player}) is true:
				set {_exit} to true
				exit loop

	while {_player} isn't on ground:
		if getChar({_player}) = "퀘이커":
			setCooldown("%{_player}'s id%-stop", 2 ticks)
			set {_axis} to getPitchAxis({_player})
			set {_ploc} to location of {_player}'s head
			set pitch of {_ploc} to 0
			set {_loc} to location of {_ploc}
			set {_dir} to vMul(getYawAxis({_loc}), -0.3)
			rotate {_dir} around {_axis} by 140 degrees
			set {_i} to 0

			while distance between {_ploc} and {_loc} < skill({_player}, "평::사거리"):
				add 1 to {_i}
				set {_loc} to {_loc}~{_dir}
				if {_i} = 5:
					particleRGB(location 0.5 down {_loc}, 0.2, 0.2, 0.2, 200, 200, 200, 10)
				if {_i} = 7:
					particleRGB(location 0.5 down {_loc}, 0.2, 0.2, 0.2, 200, 200, 200, 10)
				if {_i} = 9:
					particle("crit", location 0.5 down {_loc}, 0.1, 0.1, 0.1, 0.1, 20)
				if {_i} = 1:
					playSound("random.swing", {_loc}~vMul({_dir}, 4), 2, 0)
					playSound("random.gun2.shovel_leftclick", {_loc}~vMul({_dir}, 4), 2, 0.6)
			wait 1 tick
			if isStunned({_player}) is true:
				set {_exit} to true
				exit loop
		else:
			exit loop

	if getChar({_player}) = "퀘이커":
		setItemCooldown({_player}, 0 ticks)
		setItemCooldown({_player}, 20 ticks, true)
		setSkillCooldown({_player}, 1, rounded skill({_player}, "2::쿨타임"))
		if {_exit} is not set:
			playSound("entity.irongolem.hurt", {_player}, 3, 0)
			playSound("entity.player.attack.crit", {_player}, 3, 0.6)
			playSound("entity.player.attack.crit", {_player}, 3, 0.6)
			playSound("entity.zombie.attack_iron_door", {_player}, 3, 0.5)
			loop 5 times:
				set {_hrecoil} to random number between -7+loop-number to 7-loop-number
				set {_vrecoil} to random number between -6+loop-number to 6-loop-number
				changeSight({_player}, {_hrecoil}, {_vrecoil})
				wait 1 tick
				if loop-number = 1:
					퀘이커_2_발동({_player})
function 퀘이커_3(player: player):
	setSkillDuration({_player}, 2, -1)
	setItemCooldown({_player}, 16 ticks, true)
	sendSwing({_player})
	playSound("random.gun2.shovel_leftclick", {_player}, 2, 0.6)
	playSound("block.lava.extinguish", {_player}, 2, 0.8)

	loop 8 times:
		if getChar({_player}) = "퀘이커":
			setCooldown("%{_player}'s id%-stop", 2 ticks)
			wait 1 tick
			if isStunned({_player}) is true:
				set {_exit} to true
				exit loop

			loop loop-number times:
				set {_ploc} to location of {_player}'s head
				set {_loc} to location 1 forwards location 0.4 down {_ploc}
				set {_axis} to getPitchAxis({_loc})
				set {_dir} to vMul(getYawAxis({_loc}), -1)
				set pitch of {_loc} to 0
				rotate {_dir} around {_axis} by 90+(30*((loop-number-2)-4)) degrees
				particle("crit", {_loc}~vMul({_dir}, 1), 0.06, 0.06, 0.06, 0, 3)
				particle("crit", location 0.2 left {_loc}~vMul({_dir}, 1), 0.06, 0.06, 0.06, 0, 3)
				particle("crit", location -0.2 left {_loc}~vMul({_dir}, 1), 0.06, 0.06, 0.06, 0, 3)
				particleRGB({_loc}~vMul({_dir}, 0.7), 0.06, 0.06, 0.06, 200, 200, 200, 2)
				particleRGB(location 0.2 left {_loc}~vMul({_dir}, 0.7), 0.06, 0.06, 0.06, 200, 200, 200, 2)
				particleRGB(location -0.2 left {_loc}~vMul({_dir}, 0.7), 0.06, 0.06, 0.06, 200, 200, 200, 2)

	if getChar({_player}) = "퀘이커":
		setSkillCooldown({_player}, 2, rounded skill({_player}, "3::쿨타임"))
		if {_exit} is not set:
			playSound("entity.ghast.shoot", {_player}, 2, 0)
			playSound("entity.zombie.attack_iron_door", {_player}, 2, 0.7)
			playSound("entity.player.attack.sweep", {_player}, 2, 0.7)
			set {_ploc} to location of {_player}'s head
			set {_loc} to location 0.4 down {_ploc}
			set {_dir} to getLineOfSight({_player})
			set {_speed} to 15

			while distance between {_ploc} and {_loc} < skill({_player}, "3::사거리"):
				if getChar({_player}) = "퀘이커":
					add 1 to {_i}
					if isNonSolid({_loc}~{_dir}) is false:
						if pitch of {_dir} != 0:
							set pitch of {_dir} to 0
							set vector length of {_dir} to 0.25
						else:
							playSound("entity.player.attack.knockback", {_loc}, 2, 0.6)
							playSound("entity.player.attack.knockback", {_loc}, 2, 0.7)
							playSound("entity.player.attack.crit", {_loc}, 2, 0.7)
							set {_block} to block at {_loc}~{_dir}
							particleBlock(stone, {_loc}, 0.2, 0.2, 0.2, 0.2, 60)
							particle("crit", {_loc}, 0.2, 0.2, 0.2, 0.4, 50)
							meleeHit({_loc}, block at {_loc}, true)
							loop {_targets::*}:
								if getCooldown("%loop-value's id%-퀘이커-3") > 0 ticks:
									stun({_player}, loop-value, skill({_player}, "3::충돌기절시간"), {퀘이커::점수::기절-3})
									damage({_player}, loop-value, skill({_player}, "3::충돌피해량"), {_loc}, "normal-3")
									knockback(loop-value, getLine(location of ({_loc}~{_dir}), loop-value, 0.25), true)
							exit loop
					delete {_targets::*}
					delete {_pass}

					if distance between {_ploc} and {_loc} > 0.5:
						loop nearEnemys({_player}, {_loc}, 0.6):
							if canPass({_loc}, location of loop-entity's head) is true:
								if canPassShield({_player}, {_loc}, location of loop-entity's head) is true:
									if isLiving(loop-entity) is true:
										if loop-entity isn't a player:
											if loop-entity.hasAI() is false:
												continue
										if {_targets::%loop-entity%} is not set:
											set {_targets::%loop-entity%} to loop-entity
											teleport(loop-entity, {_loc})
											set fall distance of loop-entity to 0
											if getCooldown("%loop-entity's id%-퀘이커-3") = 0 ticks:
												if loop-entity is a player:
													addScore({_player}, "돌풍 강타", {퀘이커::점수::돌풍 강타})
												damage({_player}, loop-value, 1, {_loc}, "normal-3")
											knockback(loop-entity, vector 0, 0, 0, true)
											setCooldown("%loop-entity's id%-퀘이커-3", 4 ticks)
											set {_pass} to true
						if {_pass} is not set:
							loop nearEnemys({_player}, {_loc}, 0.6):
								if canPass({_loc}, location of loop-entity's head) is true:
									if getMetadata(loop-entity, "shield") is true:
										exit

					set {_loc} to {_loc}~{_dir}
					if mod({_i}, 6) = 0:
						loop 7 times:
							set {_axis} to getPitchAxis({_loc})
							set {_dir2} to vMul(getYawAxis({_loc}), -1)
							set pitch of {_loc} to 0
							rotate {_dir2} around {_axis} by 90+(30*(loop-number-4)) degrees
							particle("crit", {_loc}~vMul({_dir2}, 1), 0.06, 0.06, 0.06, 0, 3)
							particle("crit", location 0.2 left {_loc}~vMul({_dir2}, 1), 0.06, 0.06, 0.06, 0, 3)
							particle("crit", location -0.2 left {_loc}~vMul({_dir2}, 1), 0.06, 0.06, 0.06, 0, 3)
							particleRGB({_loc}~vMul({_dir2}, 0.7), 0.06, 0.06, 0.06, 200, 200, 200, 2)
							particleRGB(location 0.2 left {_loc}~vMul({_dir2}, 0.7), 0.06, 0.06, 0.06, 200, 200, 200, 2)
							particleRGB(location -0.2 left {_loc}~vMul({_dir2}, 0.7), 0.06, 0.06, 0.06, 200, 200, 200, 2)
					if mod({_i}/4, {_speed}/20) = 0:
						wait 1 tick
				else:
					exit loop
function 퀘이커_4_발동(player: player):
	loop 8 times:
		loop 4 times:
			add 1 to {_i}
			set {_ploc} to location of {_player}'s head
			set {_loc::%{_i}%} to location of {_ploc}
			set {_axis} to getYawAxis({_loc::%{_i}%})
			set {_dir::%{_i}%} to vMul(getPitchAxis({_loc::%{_i}%}), 0.5)
			rotate {_axis} around getPitchAxis({_loc::%{_i}%}) by 13*((loop-number-2)-2.5) degrees
			rotate {_dir::%{_i}%} around getPitchAxis({_loc::%{_i}%}) by 13*((loop-number-2)-2.5) degrees
			rotate {_dir::%{_i}%} around {_axis} by 90+(12*((loop-number-1)-4.5)) degrees
	particle("crit", location 0.3 down location 1.5 horizontally forwards {_player}'s head, 0.2, 0.2, 0.2, 0.6, 100)

	loop 60 times:
		if getChar({_player}) = "퀘이커":

			loop 8*4 times:
				set {_loc} to {_loc::%loop-number-2%}
				set {_dir} to {_dir::%loop-number-2%}
				if isNonSolid({_loc}~{_dir}) is false:
					delete {_loc::%loop-number-2%}
				if distance between {_ploc} and {_loc} >= skill({_player}, "4::사거리"):
					delete {_loc::%loop-number-2%}

				if {_loc::%loop-number-2%} is set:
					if distance between {_ploc} and {_loc} > 1:
						set {_target} to nearEnemy({_player}, {_loc}, 1.2)
						if {_target} is set:
							if canPass({_loc}, location of {_target}'s head) is true:
								if getMetadata({_target}, "shield") is true:
									set {_tid} to getMetadata({_target}, "shieldNo")
								else:
									set {_tid} to {_target}'s id
								if {_targets::%{_tid}%} is not set:
									set {_targets::%{_tid}%} to {_target}
									if {_target} is a player:
										setCooldown("%{_player}'s id%-sup-%{_target}'s id%-normal-4", parseTimespan(skill({_player}, "4::속박시간")*20))
									stun({_player}, {_target}, skill({_player}, "4::기절시간"), {퀘이커::점수::기절-4})
									leash({_player}, {_target}, skill({_player}, "4::속박시간"))
									damage({_player}, {_target}, skill({_player}, "4::피해량"), {_target}, "normal-4", false, false)
									nohear({_target}, skill({_player}, "4::속박시간"), true)
									knockback({_target}, getLine(location 1 down {_player}, {_target}, 3))
									particle("crit", location (hitbox length of {_target}/2) up {_target}, 0, 0, 0, 0.4, 60)
								if getMetadata({_target}, "shield") is true:
									delete {_loc::%loop-number-2%}
									continue

					set {_loc::%loop-number-2%} to {_loc}~{_dir}
					set {_sdir} to vSpread({_dir}, 15)
					set {_x} to x of {_sdir}
					set {_y} to y of {_sdir}
					set {_z} to z of {_sdir}
					if mod(loop-number-1, 2) = 0:
						particle("explosion", {_loc::%loop-number-2%}, {_x}, {_y}, {_z}, 1, 0)
						particle("crit", {_loc::%loop-number-2%}, 0.2, 0.2, 0.2, 0.1, 2)
					# particleBlock({_block}, {_loc::%loop-number-2%}, 0.3, 0, 0.3, 0.15, 3)
			if mod(loop-number, 3) = 0:
				wait 1 tick
function 퀘이커_4(player: player):
	if isUsingSkill({_player}, 0) is false:
		alert({_player}, "[%{퀘이커::액티브::이름1}%] 를 활성화한 상태에서만 사용할 수 있습니다.")
		exit
	setSkillDuration({_player}, 3, -1)
	setItemCooldown({_player}, 30 ticks, true)
	sendSwing({_player})
	useUlt({_player})

	loop 8 times:
		if getChar({_player}) = "퀘이커":
			set {_ploc} to location of {_player}'s head
			set {_loc} to location of {_ploc}
			set {_axis} to getYawAxis({_loc})
			set {_dir} to vMul(getPitchAxis({_loc}), 0.3)
			rotate {_dir} around {_axis} by loop-number*20 degrees
			set {_i} to 0

			while distance between {_ploc} and {_loc} < skill({_player}, "평::사거리"):
				add 1 to {_i}
				if isNonSolid({_loc}~{_dir}) is false:
					set {__loc} to location of {_loc}
					set {_loc} to {_loc}~{_dir}
					loop 4 times:
						if isNonSolid({__loc}~vMul({_dir}, 0.2)) is true:
							set {__loc} to {__loc}~vMul({_dir}, 0.2)
					playSound("entity.player.attack.strong", {__loc}, 0.4, 0.8)
					meleeHit({__loc}, block at {_loc}, true)
					exit loop

				set {_loc} to {_loc}~{_dir}
				if {_i} = 5:
					particleRGB(location 0.3 down {_loc}, 0.2, 0.2, 0.2, 200, 200, 200, 10)
				if {_i} = 7:
					particleRGB(location 0.3 down {_loc}, 0.2, 0.2, 0.2, 200, 200, 200, 10)
				if {_i} = 9:
					particle("crit", location 0.3 down {_loc}, 0.1, 0.1, 0.1, 0.1, 20)
				if {_i} = 1:
					if mod(loop-number, 2) = 0:
						playSound("random.swing", {_loc}~vMul({_dir}, 4), 2, 0)
						playSound("random.gun2.shovel_leftclick", {_loc}~vMul({_dir}, 4), 2, 0.6)

			if loop-number = 1:
				wait 2 tick
			if loop-number = 2 or 3 or 6 or 7:
				wait 1 tick
			if isStunned({_player}) is true:
				exit loop

	if getChar({_player}) = "퀘이커":
		setSkillCooldown({_player}, 3, -1)
		if {_exit} is not set:
			playSound("entity.zombie.attack_iron_door", {_player}, 6, 0.5)
			playSound("entity.player.attack.strong", {_player}, 6, 0.7)
			playSound("entity.player.attack.crit", {_player}, 6, 0.7)
			playSound("random.explosion", {_player}, 6, 1.6)
			playSound("random.explosion_reverb", {_player}, 8, 1.4)
			퀘이커_4_발동({_player})
			loop 6 times:
				set {_hrecoil} to random number between -10+loop-number to 10-loop-number
				set {_vrecoil} to random number between -8+loop-number to 8-loop-number
				changeSight({_player}, {_hrecoil}, {_vrecoil})
				wait 1 tick
