command /방화벽 [<text>] [<text>]:
	trigger:
		if player is an op:
			if arg 1 is not set:
				send "&7=================================================="
				send "&a&l/방화벽 활성화 <사유> - &a모든 플레이어가 서버에 접속할 수 없게 합니다."
				send "&a&l/방화벽 제외 <플레이어> - &a해당 플레이어는 방화벽을 무시하고 접속할 수 있습니다."
				send "&a&l/방화벽 비활성화 - &a방화벽을 해제합니다."
				send "&7=================================================="
			if arg 1 is "비활성화":
				set {connect} to false
				broadcast "&b&l[ 방화벽 ] &e&n%player%&f님이 서버 방화벽을 해제했습니다."
				broadcast "&b&l[ 방화벽 ] &a이제 서버 접속이 가능합니다."
			if arg 1 is "활성화":
				if arg 2 is set:
					set {connect} to true
					set {connectreason} to arg 2
					broadcast "&b&l[ 방화벽 ] &e&n%player%&f님이 서버 방화벽을 활성화했습니다."
					broadcast "&b&l[ 방화벽 ] &c이제 서버 접속이 불가능합니다."
				else:
					send "&a&l/방화벽 활성화 <사유>"
			if arg 1 is "제외":
				if arg 2 is set:
					loop {connectplayer::*}:
						if loop-value is arg 2:
							delete {connectplayer::%arg 2%}
							send "&b&l[ 방화벽 ] &e&n%arg 2%&f님을 방화벽 제외 대상에서 제거했습니다."
							exit
					set {connectplayer::%arg 2%} to arg 2
					send "&b&l[ 방화벽 ] &e&n%arg 2%&f님을 방화벽 제외 대상에 추가했습니다."
				else:
					send "&b&l[ 방화벽 ] &f제외된 플레이어들 : &e%{connectplayer::*}%"
					send "&a&l/방화벽 제외 <플레이어>"
		else:
			send "&b&l[ 방화벽 ] &c권한이 없습니다."
on connect:
	if {connect} is true:
		loop {connectplayer::*}:
			if "%loop-value%" is player's name:
				exit
		kick the player due to "&3&l[ &b&lDMGR &3&l] &6%colored {connectreason}%"